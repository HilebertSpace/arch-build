# Maintainer: Slava Ganzin <slava.ganzin@gmail.com>

pkgname='await'
pkgver='1.0.5'
pkgrel='1'
pkgdesc="28kb small memory footprint single binary that run list of commands in parallel and waits for their termination"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('glibc')
makedepends=('git' 'gcc' 'make')
url="https://github.com/slavaGanzin/${pkgname}"
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('74918b513dc0e45e45f181d423afe3cd2572cbc0cbd27ca4ac02002aca3706e51b5215188e0a510dc59e65e20fb9e7ab6748aff0ec50f7edcdfcf6c5b3b81a66')

build() {
  cd "${pkgname}-${pkgver}"
  gcc "${pkgname}.c" -lpthread -o "${pkgname}"
  mkdir -p autocompletions 2>/dev/null
  "./${pkgname}" --autocomplete-bash > "autocompletions/${pkgname}.bash"
  "./${pkgname}" --autocomplete-zsh > "autocompletions/${pkgname}.zsh"
  "./${pkgname}" --autocomplete-fish > "autocompletions/${pkgname}.fish"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 0755 "${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 0644 "autocompletions/${pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm 0644 "autocompletions/${pkgname}.zsh" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm 0644 "autocompletions/${pkgname}.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}
