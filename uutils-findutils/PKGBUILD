# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_pkgname="findutils"
pkgname="uutils-${_pkgname}"
pkgver='0.6.0'
pkgrel='1'
pkgdesc="Rust implementation of findutils"
arch=('x86_64')
url="https://github.com/uutils/${_pkgname}"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('rust')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('9d0e76e5c5651dca75607b1eb1b0c2b3192768aca132210c873dd701a68984e40833b170d5f971d92b555f0f67f0477c36ab779462bcf8abc209a6f9f9fed86b')

prepare() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "${_pkgname}-${pkgver}"
    install -Dm 0755 target/release/find "${pkgdir}/usr/bin/uu-find"
    install -Dm 0755 target/release/xargs "${pkgdir}/usr/bin/uu-xargs"
    install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
