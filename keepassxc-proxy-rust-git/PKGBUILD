# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_name='keepassxc-proxy'
_pkgname="${_name}-rust"
pkgname="${_pkgname}-git"
pkgver='r34.0b81215'
pkgrel='1'
pkgdesc='Application that works as a proxy between Native Messaging browser extension and KeePassXC'
arch=('x86_64')
url="https://github.com/varjolintu/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'rust')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "${_pkgname}"
    install -Dm 0755 "target/release/${_name}" "${pkgdir}/usr/bin/${_pkgname}"
}
