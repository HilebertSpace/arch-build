# Maintainer: asm0dey <pavel.finkelshtein@gmail.com>

pkgname='fselect'
pkgver='0.8.5'
pkgrel='1'
pkgdesc='Find files with SQL-like queries'
arch=('i686' 'x86_64')
url="https://github.com/jhspetersson/fselect"
license=("MIT")
depends=('gcc-libs')
makedepends=('rust')
provides=('fselect')
conflicts=('fselect-git')
options=(!lto)
source=("$pkgver.tar.gz::https://github.com/jhspetersson/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('263511863e6c1d6b1c7827ae483628e2c2096c3da060c4f163899eb32cc7c550c3a1882f0ff5aab541b2cc9d3fb8f7bc8145638d2d7e66801b59b227b64a9586')

prepare() {
    cd "${pkgname}-${pkgver}"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${pkgname}-${pkgver}"
    cargo build --frozen --release #--all-features
}

check() {
    cd "${pkgname}-${pkgver}"
    cargo test --frozen #--all-features
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm 0755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm 0644 'LICENSE-MIT' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
