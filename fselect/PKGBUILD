# Maintainer: asm0dey <pavel.finkelshtein@gmail.com>

pkgname='fselect'
pkgver='0.8.5'
pkgrel='1'
pkgdesc='Find files with SQL-like queries'
arch=('i686' 'x86_64')
url="https://github.com/jhspetersson/${pkgname}"
license=('MIT OR Apache-2.0')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::https://static.crates.io/crates/${pkgname}/${pkgname}-${pkgver}.crate")
sha512sums=('2833a617f4182386361ca56bd39179e76f011097c57976e8acd8bd1986651112619a6a5f8121a5718eec29d5a1eff0566debedc4ca493de153d3b2b140ccafb7')

prepare() {
    cd "${pkgname}-${pkgver}"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${pkgname}-${pkgver}"
    CFLAGS+=' -ffat-lto-objects'
    cargo build --frozen --release --all-features
}

check() {
    cd "${pkgname}-${pkgver}"
    cargo test --frozen --all-features
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm 0755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm 0644 'LICENSE-MIT' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
