# Maintainer: Shapiro <shapiro at quantentunnel dot de>

pkgname='veracrypt-inyourlanguage'
_pkgname='VeraCrypt'
pkgver='1.26.7'
pkgrel='8'
pkgdesc='Disk encryption with strong security based on TrueCrypt 7.1a. Supporting 40+ languages for installation.'
url='https://www.veracrypt.fr'
arch=(
  'aarch64'
  'arm64'
  'armhf'
  'i686'
  'x86_64'
)
license=('LicenseRef-VeraCrypt')
provides=('veracrypt')
conflicts=(
  'veracrypt'
  'veracrypt-console-bin'
  'veracrypt-git'
  'veracrypt-trans'
)
depends=(
  'bash'
  'device-mapper'
  'fuse2>=2.8.0'
  'gcc-libs'
  'glibc'
  'libsm'
  'wxwidgets-common>=3.2.4'
  'wxwidgets-gtk3>=3.2.4'
)
makedepends=(
  'coreutils'
  'git'
  'libxml2'
  'pcsclite'
  'yasm'
)
optdepends=(
  'sudo: mounting encrypted volumes as nonroot users'
  'pcsclite: providing support for smartcards'
)
source=("https://launchpad.net/veracrypt/trunk/${pkgver}/+download/VeraCrypt_${pkgver}_Source.tar.bz2{,.sig}")
sha512sums=(
  'c803c6301a8a7b6d8efc74284070c01629a760dceaa518f2287d62709bd25450fec75f2fa0e2ab0eb57993faec9cba4d51104273a56a88fa288a1e14bcf868bd'
  '2cef9ccf24fdb032a3867bbad73576ed213c589a23e1e24e5e7ad7e9e6ade8c48a320b02e31c95b484701ba20e551583b60f14a8c6e2c930b9dbfda215d5ed7b'
)
validpgpkeys=('5069A233D55A0EEB174A5FC3821ACD02680D16DE') # VeraCrypt Team <veracrypt@idrix.fr>

build() {
  cd "${_pkgname}_${pkgver}/src"
  make PKG_CONFIG_PATH='/usr/lib/pkgconfig' \
       WX_CONFIG='/usr/bin/wx-config' \
       TC_EXTRA_LFLAGS="${TC_EXTRA_LFLAGS} -ldl ${LDFLAGS}" \
       TC_EXTRA_CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}" \
       TC_EXTRA_CFLAGS="${CFLAGS} ${CPPFLAGS}"
}

package() {
  cd "${_pkgname}_${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/veracrypt"
  rm "${pkgdir}/usr/bin/veracrypt-uninstall.sh"
  rm "${pkgdir}/usr/share/doc/veracrypt/License.txt"
}
