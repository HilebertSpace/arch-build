# Maintainer: Luis PÃ©rez <luis.perez@protonmail.com>
# Contributor: djmattyg007 <internet@matthewgamble.net>

_pkgname=doggo
pkgname=doggo-test
pkgver=1.0.4
pkgrel=1
pkgdesc='Command-line DNS Client for Humans.'
arch=('x86_64')
url="https://github.com/mr-karan/doggo"
license=('GPL')
makedepends=('go')
source=("${url}/archive/v${pkgver}.tar.gz")
conflicts=('doggo')
provides=('doggo')
sha256sums=('d7b8c742680332b172ad64e4d625449537c89e2607b7d7fd83a34fdd737c039f')

prepare(){
  cd "$_pkgname-$pkgver"
  mkdir -p build/
}

build() {
  cd "$_pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  BUILD_DATE=$(date '+%Y-%m-%d %H:%M:%S')
  go build -o build/$_pkgname -ldflags="-X 'main.buildVersion=${pkgver}' -X 'main.buildDate=${BUILD_DATE}'" ./cmd/doggo/
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm755 build/"$_pkgname" "$pkgdir"/usr/bin/"$_pkgname"
  install -Dm644 README.md "$pkgdir"/usr/share/doc/"$_pkgname"/README.md
}
