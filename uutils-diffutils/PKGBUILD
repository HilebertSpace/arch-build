# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname='diffutils'
pkgname="uutils-${_pkgname}"
pkgver='0.4.1'
pkgrel='1'
pkgdesc='Drop-in replacement of diffutils in Rust'
arch=('x86_64')
url='https://uutils.github.io'
license=('Apache-2.0 OR MIT')
depends=('gcc-libs' 'glibc')
makedepends=('rust')
checkdepends=('ed')
source=("${pkgname}-${pkgver}.tar.gz::https://static.crates.io/crates/${_pkgname}/${_pkgname}-${pkgver}.crate")
sha512sums=('95108754e79636bb48b019c045a511d88f3eddadd68684ed4f017f90c37bcf767c5b60448b70123cbf3df940e0a789c46065f52d5c9a6a28c86949c1c7c15cac')

prepare() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN='stable'
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN='stable'
    export CARGO_TARGET_DIR='target'
    cargo build --frozen --release --all-features
}

check() {
    cd "${_pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN='stable'
    cargo test --frozen --all-features
}

package() {
    cd "${_pkgname}-${pkgver}"
    install -Dm 0755 "target/release/${_pkgname}" "$pkgdir/usr/bin/uu-${_pkgname}"
    install -Dm 0644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
