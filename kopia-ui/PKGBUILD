# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Jens JÃ¤schke <arch [at] jensjaeschke [dot] de>

_pkgname='kopia'
pkgname="${_pkgname}-ui"
pkgdesc='A cross-platform backup-tool with encryption, deduplication, compression and cloud support.'
pkgver='0.15.0'
pkgrel='1'
arch=('x86_64')
url="https://${_pkgname}.io"
license=('APACHE')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=('electron' 'nodejs')
optdepends=('fuse3: Needed to mount Kopia snapshots on local filesystem')
makedepends=('go')
checkdepends=('openssh' 'inetutils')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('7000f6999ea8d1975645293c3f4f3ba25583ba13d3095aef3cf274251b36b75f')

prepare() {
    cd "${_pkgname}-${pkgver}/app"
    npm install
}

build() {
    cd "${_pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -ldflags "-linkmode=external -X github.com/${_pkgname}/${_pkgname}/repo.BuildVersion=${pkgver}-${pkgrel} -X github.com/${_pkgname}/${_pkgname}/repo.BuildInfo=unofficial -X github.com/${_pkgname}/${_pkgname}/repo.BuildGitHubRepo=ARCH_USER_REPOSITORY"
    npm run build-electron -- --linux --x64 --dir app -c.electronDist='/usr/lib/electron' -c.electronVersion="$(</usr/lib/${_electron}/version)"
}

check() {
    cd "${_pkgname}-${pkgver}"
    make test
}

package() {
    cd "${_pkgname}-${pkgver}"
    install -Dm755 "${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    "${_pkgname}" --completion-script-bash | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
    "${_pkgname}" --completion-script-zsh | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
