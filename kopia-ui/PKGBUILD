# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Jens JÃ¤schke <arch [at] jensjaeschke [dot] de>

pkgname='kopia-ui'
_pkgname='kopia'
pkgdesc='A cross-platform backup-tool with encryption, deduplication, compression and cloud support.'
pkgver='0.15.0'
pkgrel='1'
arch=('x86_64')
url='https://kopia.io'
license=('APACHE')
depends=('electron' 'nodejs')
optdepends=('fuse3: mounting snapshots locally')
makedepends=('go')
checkdepends=('openssh' 'inetutils')
provides=("${pkgname}")
conflicts=("${pkgname}-bin")
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/kopia/kopia/archive/refs/tags/v${pkgver}.tar.gz"
    'kopia-ui'
)
sha256sums=(
    '7000f6999ea8d1975645293c3f4f3ba25583ba13d3095aef3cf274251b36b75f'
    'a010a381b8cca6d0c3b4e470607ec701f51efbae74611ccc8892db62aacbeb23'
)

build() {
    cd "${_pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -o 'dist/kopia_linux_x64/kopia' -ldflags "-linkmode=external -X github.com/kopia/kopia/repo.BuildVersion="${pkgver}-${pkgrel}" -X github.com/kopia/kopia/repo.BuildInfo=unofficial -X github.com/kopia/kopia/repo.BuildGitHubRepo=ARCH_USER_REPOSITORY"
    npm --prefix app install --no-audit
    npm --prefix app run build-electron --linux --x64 --dir -c.electronDist='/usr/lib/electron' -c.electronVersion="$(</usr/lib/electron/version)"
}

check() {
    cd "${_pkgname}-${pkgver}"
    make test
}

package() {
    cd "${_pkgname}-${pkgver}"
    install -Dm 644 dist/kopia-ui/linux-unpacked "${pkgdir}/usr/lib/${pkgname}"
    install -Dm 755 '../kopia-ui' -t "${pkgdir}/usr/bin"
}
