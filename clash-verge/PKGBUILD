# Maintainer: kiri@vern.cc
# Contributor: sukanka<su975853527 AT gmail dot com>

pkgname='clash-verge'
pkgver='1.4.9'
pkgrel='1'
pkgdesc="A Clash GUI based on tauri, revived"
arch=('aarch64' 'x86_64')
url='https://github.com/clash-verge-rev/clash-verge-rev'
_url='https://github.com/MetaCubeX/meta-rules-dat'
license=('GPL3')
depends=(
    'cairo'
    'clash-meta'
    'gcc-libs'
    'gdk-pixbuf2'
    'glib2'
    'glibc'
    'gtk3'
    'hicolor-icon-theme'
    'libsoup'
    'webkit2gtk'
    'openssl'
)
makedepends=(
    'cargo-tauri'
    'jq'
    'moreutils'
    'rust'
    'pnpm'
)
source=(
    "${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
    "${pkgname}-${pkgver}-country.mmdb::${_url}/releases/download/latest/country.mmdb"
    "${pkgname}-${pkgver}-geosite.dat::${_url}/releases/download/latest/geosite.dat"
    "${pkgname}-${pkgver}-geoip.dat::${_url}/releases/download/latest/geosite.dat"
	"${pkgname}.desktop"
	'001-disable-updater.patch'
)

sha512sums=(
    '580a751827cc9fdf35b3dd72df06f2011ac99af8355a58223dfeb19a73f81eaa845b4912271b4d79c9d46e614201b06d7a3afb08ccbb29c8ba8a5242ef38d7fb'
    'SKIP'
    'SKIP'
    'SKIP'
    '2066dacf2e5e0135e6403cbfb825efcdf08bbcdc781407e6bb1fbb85143817b2b1abef641d20390ff7e5b3e91a509933e9eb17a64f9de7671445ac6d5363a44a'
    'd1e8992cc0de168930002adccf7953416dfac199ea11886043de58b0df366e4337d5ec548fa3bf8be30575596a5e71dd31960db72f9e067ff131feb756bc96c1'
)
options=(!lto)

prepare() {
	mv "${pkgname}-rev-${pkgver}" "${pkgname}-${pkgver}"
	cd "${pkgname}-${pkgver}"
	patch --strip=1 < '../001-disable-updater.patch'

	install -d 'src-tauri/sidecar'
	install -d 'src-tauri/resources'
	# empty files as placeholders
	touch "src-tauri/sidecar/clash{,-meta-alpha,-meta}-${CARCH}-unknown-linux-gnu"
	touch 'src-tauri/resources/Country.mmdb'
	touch 'src-tauri/resources/geo{ip,site}.dat'

	jq 'del(.scripts.prepare)' 'package.json' | sponge 'package.json'

	cd 'src-tauri'
	# only build the excutable
	jq '.tauri.bundle.active = false' 'tauri.conf.json' | sponge 'tauri.conf.json'
	# disable updater
	jq '.tauri.updater.active = false' 'tauri.conf.json' | sponge 'tauri.conf.json'

}

build() {
	cd "${pkgname}-${pkgver}"
	# export HOME=$srcdir
	pnpm install
	pnpm run check
	cargo-tauri build
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "src-tauri/target/release/${pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm 644 "../${pkgname}-${pkgver}-country.mmdb" "${pkgdir}/usr/lib/${pkgname}/resources/Country.mmdb"
    install -Dm 644 "../${pkgname}-${pkgver}-geosite.dat" "${pkgdir}/usr/lib/${pkgname}/resources/geosite.dat"
    install -Dm 644 "../${pkgname}-${pkgver}-geoip.dat" "${pkgdir}/usr/lib/${pkgname}/resources/geoip.dat"
	install -Dm 644 'src/assets/image/logo.svg' "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
	install -Dm 644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
}