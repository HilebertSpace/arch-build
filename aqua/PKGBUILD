# Maintainer: Luis PÃ©rez <luis.perez@protonmail.com>
# Contributor: djmattyg007 <internet@matthewgamble.net>

_commit='1e48dedbcb06a39b5f61615ec9f03866c4aecb4c'
pkgname='aqua'
pkgver='2.30.0'
pkgrel='1'
pkgdesc='Declarative CLI Version manager written in Go.'
arch=('x86_64')
url="https://github.com/aquaproj/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('c76a0026f04e551109b519a493109cd9b23b8f21345b86fb46738d0e05d1b333620fb885b679c2a3f1b6db47417fb8128d34e2719bef2f143afc69e5a336cf34')

prepare(){
  cd "${pkgname}-${pkgver}"
  mkdir build completions
  go mod vendor
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS='-buildmode=pie -mod=vendor -modcacherw'
  export GOENV='off'
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"
  ls -la
  go build -ldflags "-compressdwarf=false -linkmode=external -X 'main.version=${pkgver}' -X 'main.commit=${_commit}' -X 'main.date=${BUILD_DATE}'" -o build ./cmd/...
  "build/${pkgname}" completion bash > completions/bash
  "build/${pkgname}" completion zsh > completions/zsh
  "build/${pkgname}" completion fish > completions/fish
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 0755 "build/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 0644 "completions/bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm 0644 "completions/zsh" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm 0644 "completions/fish" "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}
