# Maintainer: Daniel Menelkir <dmenelkir at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname='peruse'
pkgver='1.80'
pkgrel='1'
pkgdesc='Comic book viewer and creator based on KDE Frameworks 5'
arch=('x86_64')
url="https://${pkgname}.kde.org"
license=('LGPL-2.1-or-later')
depends=(
    'baloo'
    'kconfig'
    'kiconthemes'
    'kio-extras'
    'kirigami2'
    'knewstuff5'
    'plasma-framework5'
    'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('okular: for cbr, cb*, pdf, deja-vu, epub file support')
source=("https://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('3c6188780b11743abd465c9cb7eadce94e638d292fe77e674068c2c123dbb614d1d46ba984a20b30a03e3e8b0ac941f6ec169cc52b4c2c83d14649c3d047156f')

build() {
    cmake -B build -S "${pkgname}-${pkgver}" -DKDE_INSTALL_USE_QT_SYS_PATHS='ON'
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
}
