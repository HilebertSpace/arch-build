# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkgname='kmplayer'
pkgname='kmplayer-git'
pkgver='r2839.0703e60-1'
pkgrel='1'
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=('x86_64')
license=(GPL LGPL FDL)
url="https://${_pkgname}.kde.org"
depends=(
    'bash'
    'cairo'
    'gcc-libs'
    'glibc'
    'hicolor-icon-theme'
    'kbookmarks5'
    'kcompletion5'
    'kconfig5'
    'kconfigwidgets5'
    'kcoreaddons5'
    'kdelibs4support'
    'ki18n5'
    'kiconthemes5'
    'kio5'
    'kmediaplayer'
    'kparts5'
    'ktextwidgets5'
    'kwidgetsaddons5'
    'kxmlgui5'
    'libxcb'
    'phonon-qt5'
    'qt5-base'
    'qt5-svg'
    'qt5-x11extras'
)
makedepends=('appstream' 'extra-cmake-modules' 'kdesignerplugin' 'kdoctools5' 'kinit')
optdepends=('mplayer: mplayer backend')
source=("git+https://invent.kde.org/multimedia/${_pkgname}")
sha512sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    export QT_SELECT='5'
    cmake -B build -S "${_pkgname}" -Wno-dev
    cmake --build build
}

check() {
    export QT_SELECT='5'
    ctest --test-dir build --output-on-failure
}

package() {
    export QT_SELECT='5'
    DESTDIR="${pkgdir}" cmake --install build
}
